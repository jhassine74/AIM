urnModel Example {
description "This-is-an-example."
author "ITU-T"
created "Feb-20-2017"
modified "Feb-20-2017"
version "1.0"
urnVersion "29.0"
}

actor ServiceProvider#""{

	softgoal MinimumCost#""{
		importance 50
	}
	
	softgoal MaximumPerformance#""{
		importance 100
	}
	
	softgoal MinimumChangestoInfrastructures#""{
		contributesTo MinimumCost with 25
	}
	goal MaximumHardwareUtilization#""{
		contributesTo MinimumChangestoInfrastructures correlated with 25
		and decomposes MaximumPerformance
	}
	
	goal MaximumThroughput#""{
		and decomposes MaximumPerformance
	}
	
	softgoal MinimumMessageExchange#""{
		contributesTo MaximumThroughput with 75
	}
	
	softgoal MinimumSwitchLoad#""{
		contributesTo MaximumThroughput with 25
	}
}
actor System#""{
	goal DetermineDataLocation#""{
		importance 100
	}
	goal DetermineServiceLocation#""{
		importance 100
	}
	
	task ServiceInControlSwitch#""{
		or decomposes DetermineServiceLocation
		contributesTo ServiceProvider.MinimumMessageExchange with 100
		contributesTo ServiceProvider.MinimumSwitchLoad correlated with -100
	}
	task ServiceInServiceControlPoint#""{
		or decomposes DetermineServiceLocation
		contributesTo ServiceProvider.MinimumMessageExchange with -75
		contributesTo ServiceProvider.MinimumSwitchLoad with 100
	}
	
	task DataInNewServiceNode#""{
		or decomposes DetermineDataLocation
		contributesTo ServiceProvider.MinimumChangestoInfrastructures with -25 
		dependsOn Vendor.ServiceNode
	}

	task DataInServiceControlPoint#""{
		or decomposes DetermineDataLocation
		contributesTo ServiceProvider.MinimumChangestoInfrastructures with 25
	}

	task InstallServiceNode#""{
		and decomposes DataInNewServiceNode
	}
}

actor Vendor#""{
	goal ServiceNodesReadyforSale#""{
		importance 100
	}
	resource ServiceNode#""{
		dependsOn ServiceNodesReadyforSale
	}
}